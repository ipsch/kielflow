# File for ploting ion- and electron density in 1d via plot4

#PLOT -l
#PLOT -s 12
#PLOT -w 16 -h 16

########## quick Options ######################################################
if (!exists("filename")) filename="./data/plot_data.dat"
Xrange = 6.
Yscale = 7.
Y2scale = 0.1

########### PLOT Options ######################################################
set xzeroaxis
set yzeroaxis

########## Ranges #############################################################
#set xrange[-Xrange:Xrange]
set yrange[1.-Yscale:1.+Yscale]
set y2range[1.-Y2scale:1.+Y2scale]

########## Labels #############################################################
set xlabel  'x in $\lambda_{De}$'    offset  0, .6
set ylabel  'ion-density $n_i$'      offset   1.8,0
set y2label 'electron-density $n_e$' offset  -1.6,0
set title 'charge-densities'         offset  0,-.8

set ytics nomirror
set y2tics
set tics in

########## Canvas #############################################################
# reference is left buttom
set lmargin at screen .15
set rmargin at screen .85
set bmargin at screen .15
set tmargin at screen .85

########## Style ##############################################################
set style line 1 linetype 1 linewidth 5 linecolor rgb '#ff0000'
set style line 2 linetype 1 linewidth 5 linecolor rgb '#0000ff'
set style line 3 linetype 2 linewidth 5 linecolor rgb '#7777ff'


########## Plot ###############################################################
plot filename using 1:7 with lines ls 1 title "$n_i$" axes x1y1,\
     filename using 1:(exp($8)) with lines ls 2 title "$n_e$" axes x1y2,\
     "./data/nd1d.dat" using 1:(-($4)) with lines ls 3 title "$n_d$" axes x1y1

     