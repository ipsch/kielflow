# File for ploting electric-potential in 1d via plot4

#PLOT -l
#PLOT -s 12
#PLOT -w 16 -h 16

########## quick Options ######################################################
if (!exists("filename")) filename="./data/plot_data.dat"
Xrange = 6.
Yrange = .04
Phi_mV(Phi) = 2.6026*1000*Phi # potential in mV

########### PLOT Options ######################################################
set xzeroaxis
set yzeroaxis

########## Ranges #############################################################
#set xrange[-Xrange:Xrange]
set yrange[-Yrange:Yrange]
set y2range [Phi_mV(-Yrange):Phi_mV(Yrange)]
#set autoscale y2

########## Labels #############################################################
set xlabel 'x in $\lambda_{De}$' offset  0, .6
set ylabel "Potential in $\\frac{q_i}{k_b T_e}$" offset   1.8,0
set y2label 'Potential in mV'    offset  -1.6,0
set title 'Potential $\Phi$'     offset  0,-.8

set ytics nomirror
set y2tics
set tics in

########## Canvas #############################################################
# reference is left buttom
set lmargin at screen .15
set rmargin at screen .85
set bmargin at screen .15
set tmargin at screen .85

########## Style ##############################################################
set style line 1 linetype 1 linewidth 5 linecolor rgb '#ff0000'
set style line 2 linetype 1 linewidth 5 linecolor rgb '#0000ff'
set style line 3 linetype 3 linewidth 2 linecolor rgb '#0000ff'
set style line 4 linetype 1 linewidth 5 linecolor rgb '#777777'
set style line 5 ps 0 linetype 1 linewidth 0.5 lc rgb "#ff7777"

########## Plot ###############################################################
plot filename using 1:8 with line ls 1 axes x1y1 notitle, \
     filename using 1:(Phi_mV($8)) with line ls 1 axis x1y2 notitle

     